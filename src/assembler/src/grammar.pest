/// Link to docs: https://docs.rs/pest_derive/2.1.0/pest_derive/
/// Link to book: https://pest.rs/book/
WHITESPACE = _{" " | "\t"}
COMMENT = _{WHITE_SPACE* ~ (("/*" ~ (!("*/") ~ ANY)* ~ "*/") | ("//" ~ (!(NEWLINE) ~ ANY)* ~ &(NEWLINE | EOI)))}

file = {SOI ~ WHITE_SPACE* ~ inputs ~ program ~ WHITE_SPACE* ~ EOI}
program = {start_of_program ~ action+}
end_of_line = _{&end_of_line_impl}
end_of_line_impl = ${COMMENT? ~ (NEWLINE | EOI)}
hex_prefix = {^"0x"}
bin_prefix = {^"0b"}
trailing_zeroes = _{"0"*}

inputs = ${( input ~ input_byte+ )?}
input = _{WHITE_SPACE* ~ ^"input:" ~ end_of_line}
input_byte = ${WHITE_SPACE* ~ (
    char_input
  | bin_input
  | hex_input
  | dec_input
  ) ~ end_of_line ~ !EOI
}
char_input = {("'" ~ ASCII ~ "'") | "'\\n'" | "'\\t'" | "'\\0'" | "'\\r'"}
hex_input = {hex_prefix ~ trailing_zeroes ~  ASCII_HEX_DIGIT{,2}}
bin_input = {bin_prefix ~ trailing_zeroes ~ ASCII_BIN_DIGIT{,8}}
dec_input = {(trailing_zeroes ~ ('0'..'1')? ~ ASCII_DIGIT{,2})
  | (trailing_zeroes ~ "2" ~ '0'..'4' ~ ASCII_DIGIT)
  | (trailing_zeroes ~ "25" ~ '0'..'5')
}



start_of_program = {WHITE_SPACE* ~ ^"program:" ~ end_of_line}
nop = {WHITE_SPACE* ~ ^"nop" ~ end_of_line}
j = {WHITE_SPACE* ~ ^"j" ~ end_of_line}
jez = {WHITE_SPACE* ~ ^"jez" ~ end_of_line}
jnz = {WHITE_SPACE* ~ ^"jnz" ~ end_of_line}
jgez = {WHITE_SPACE* ~ ^"jgez" ~ end_of_line}
jgz = {WHITE_SPACE* ~ ^"jgz" ~ end_of_line}
jlez = {WHITE_SPACE* ~ ^"jlez" ~ end_of_line}
jlz = {WHITE_SPACE* ~ ^"jlz" ~ end_of_line}
dec_literal = {WHITE_SPACE* ~ ( ( "6" ~ ('0'..'3') ) | ( '1'..'5'? ~ ASCII_DIGIT ) | ASCII_DIGIT ) ~ end_of_line}
bin_literal = {WHITE_SPACE* ~ bin_prefix ~ trailing_zeroes ~ ASCII_BIN_DIGIT{,6} ~ end_of_line }
hex_literal = {WHITE_SPACE* ~ hex_prefix ~ trailing_zeroes ~ '0'..'3'? ~ ASCII_HEX_DIGIT ~ end_of_line}
literal = {WHITE_SPACE* ~ dec_literal | bin_literal | hex_literal}
add = {WHITE_SPACE* ~ ^"add" ~ end_of_line}
sub = {WHITE_SPACE* ~ ^"sub" ~ end_of_line}
or = {WHITE_SPACE* ~ ^"or" ~ end_of_line}
nor = {WHITE_SPACE* ~ ^"nor" ~ end_of_line}
xor = {WHITE_SPACE* ~ ^"xor" ~ end_of_line}
xnor = {WHITE_SPACE* ~ ^"xnor" ~ end_of_line}
and = {WHITE_SPACE* ~ ^"and" ~ end_of_line}
nand = {WHITE_SPACE* ~ ^"nand" ~ end_of_line}

mov = {WHITE_SPACE* ~ ^"mov" ~ WHITE_SPACE+ ~ from ~ WHITE_SPACE+ ~ to ~ end_of_line}
from = { input_reg | ( ^"reg"? ~ '0'..'5')}
to = { output_reg | ( ^"reg"? ~ '0'..'5')}
input_reg = {^"input" | ^"in" | ^"i"}
output_reg = {^"output" | ^"out" | ^"o"}


instruction = ${nop | j | jez | jnz | jgez |jgz | jlez | jlz | literal | add | sub | or | nor | xor | xnor | and | nand | mov}
empty = {COMMENT? ~ (WHITE_SPACE | NEWLINE)+}
action = {instruction | constant | macro_call | full_macro | label | use_label_or_const | empty}

label = {WHITE_SPACE* ~ ^"label" ~ identifier ~ ":" ~ end_of_line}
identifier = ${WHITE_SPACE* ~ (ASCII_ALPHA | ident_allowed_special_chars) ~ (ASCII_ALPHANUMERIC | ident_allowed_special_chars)*}
ident_allowed_special_chars = {"_"}
use_label_or_const = {WHITE_SPACE* ~ identifier ~ end_of_line}
full_macro = {macro_def ~ macro_body ~ end_of_macro}
macro_body = {(!(end_of_macro) ~ ANY)*}
end_of_macro = {WHITE_SPACE* ~ "end_macro:" ~ end_of_line}

macro_def = ${WHITE_SPACE* ~ ^"macro" ~ WHITE_SPACE+ ~  identifier ~ WHITE_SPACE* ~ "(" ~ WHITESPACE* ~ macro_args ~ WHITESPACE* ~ "):" ~ end_of_line}
macro_args = {WHITE_SPACE* ~ (macro_arg ~ ("," ~ WHITE_SPACE* ~ macro_arg ~ WHITE_SPACE*)*)?}
macro_arg = ${"%" ~ identifier}
arg = {(!("," | ")") ~ ANY)*}
args = {WHITE_SPACE* ~ (arg ~ ("," ~ WHITE_SPACE* ~ arg ~ WHITE_SPACE*)*)?}
macro_call = ${WHITE_SPACE* ~  identifier ~ WHITE_SPACE* ~ "(" ~ WHITESPACE* ~ args ~ WHITESPACE* ~ ")" ~ end_of_line}

constant = ${WHITESPACE* ~ identifier ~ WHITESPACE* ~ "=" ~ WHITESPACE* ~ const_val}
const_val = {(literal | identifier) ~ end_of_line ~ (NEWLINE | EOI)}